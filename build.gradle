buildscript {
    dependencies {
        classpath "com.diffplug.spotless:spotless-plugin-gradle:6.19.0"
    }
}

plugins {
    id "base"
    id "jacoco"
    id "jacoco-report-aggregation"
    id "org.sonarqube" version "4.2.1.3168"
    id "io.github.gradle-nexus.publish-plugin" version "1.3.0"
}

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "goodforgod"
        property "sonar.projectKey", "GoodforGod_$artifactRootId"
        property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/testCodeCoverageReport/testCodeCoverageReport.xml"
    }
}

dependencies {
    jacocoAggregation project(":common")
    jacocoAggregation project(":jdbc")
    jacocoAggregation project(":postgres")
    jacocoAggregation project(":oracle")
    jacocoAggregation project(":mysql")
    jacocoAggregation project(":mariadb")
    jacocoAggregation project(":cockroachdb")
    jacocoAggregation project(":kafka")
    jacocoAggregation project(":cassandra")
    jacocoAggregation project(":redis")
}

reporting {
    reports {
        testCodeCoverageReport(JacocoCoverageReport) {
            testType = TestSuiteType.UNIT_TEST
        }
    }
}

allprojects {
    group = groupId
    var ver = System.getenv().getOrDefault("RELEASE_VERSION", artifactVersion)
    version = ver.startsWith("v") ? ver.substring(1) : ver

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }
}

subprojects {
    apply plugin: "jacoco"
    apply plugin: "java-library"
    apply plugin: "com.diffplug.spotless"

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    dependencies {
        compileOnly "org.jetbrains:annotations:24.0.1"

        testRuntimeOnly "ch.qos.logback:logback-classic:1.4.8"
        testRuntimeOnly libs.junit.engine
        testRuntimeOnly libs.testcontainers.junit
        testImplementation libs.junit.api
        testImplementation libs.junit.params
    }

    sonarqube {
        properties {
            property "sonar.host.url", "https://sonarcloud.io"
            property "sonar.organization", "goodforgod"
            property "sonar.projectKey", "GoodforGod_testcontainers-extensions-${project.name}"
        }
    }

    test {
        useJUnitPlatform()
        testLogging {
            events("passed", "skipped", "failed")
            exceptionFormat("full")
            showStandardStreams(false)
        }

        reports {
            html.required = false
            junitXml.required = false
        }
    }

    spotless {
        java {
            encoding("UTF-8")
            importOrder()
            removeUnusedImports()
            eclipse("4.21").configFile("${rootDir}/config/codestyle.xml")
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding("UTF-8")
        options.incremental(true)
        options.fork = true
    }

    check.dependsOn jacocoTestReport
    jacocoTestReport {
        reports {
            xml.required = true
            html.destination file("${buildDir}/jacocoHtml")
        }
    }

    javadoc {
        options.encoding = "UTF-8"
        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption("html5", true)
        }
    }
}

nexusPublishing {
    packageGroup = groupId
    repositories {
        sonatype {
            username = System.getenv("OSS_USERNAME")
            password = System.getenv("OSS_PASSWORD")
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

subprojects {
    apply plugin: "maven-publish"
    apply plugin: "org.sonarqube"

    sonarqube {
        properties {
            property "sonar.host.url", "https://sonarcloud.io"
            property "sonar.organization", "goodforgod"
            property "sonar.projectKey", "GoodforGod_$artifactRootId"
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifactId = "$artifactRootId-${project.name}"

                var artifactName = artifactId
                pom {
                    url = "https://github.com/GoodforGod/$artifactRootId"
                    name = artifactName
                    description = "Testcontainers ${artifactName.capitalize()} Extension with advanced testing features"

                    license {
                        name = "Apache License 2.0"
                        url = "https://github.com/GoodforGod/$artifactRootId/blob/master/LICENSE"
                        distribution = "repo"
                    }

                    developer {
                        id = "GoodforGod"
                        name = "Anton Kurako"
                        email = "goodforgod.dev@gmail.com"
                        url = "https://github.com/GoodforGod"
                    }

                    scm {
                        connection = "scm:git:git://github.com/GoodforGod/${artifactRootId}.git"
                        developerConnection = "scm:git:ssh://GoodforGod/${artifactRootId}.git"
                        url = "https://github.com/GoodforGod/$artifactRootId/tree/master"
                    }
                }
            }
        }
        repositories {
            maven {
                name = "OSSRH"
                def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
                def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username System.getenv("OSS_USERNAME")
                    password System.getenv("OSS_PASSWORD")
                }
            }
            if (!version.endsWith("SNAPSHOT")) {
                maven {
                    name = "GitHubPackages"
                    url = "https://maven.pkg.github.com/GoodforGod/$artifactRootId"
                    credentials {
                        username = System.getenv("GITHUB_ACTOR")
                        password = System.getenv("GITHUB_TOKEN")
                    }
                }
            }
        }
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    if (project.hasProperty("signingKey")) {
        apply plugin: "signing"
        signing {
            def signingKey = findProperty("signingKey")
            def signingPassword = findProperty("signingPassword")
            useInMemoryPgpKeys(signingKey, signingPassword)
            sign publishing.publications.mavenJava
        }
    }
}